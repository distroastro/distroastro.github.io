#!/usr/bin/perl -w

################################################################################
#                                                                              #
# Distro Astro Location                                                        #
# Version 4.0                                                                  #
#                                                                              #
# Copyright (C) 2014-2017 Bamm Gabriana.                                       #
#                                                                              #
# This program is free software; you can redistribute it and/or modify         #
# it under the terms of the GNU General Public License as published by         #
# the Free Software Foundation; either version 2 of the License, or            #
# (at your option) any later version.                                          #
#                                                                              #
# This program is distributed in the hope that it will be useful,              #
# but WITHOUT ANY WARRANTY; without even the implied warranty of               #
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the                #
# GNU General Public License for more details.                                 #
#                                                                              #
# You should have received a copy of the GNU General Public License            #
# along with this program. If not, see <http://www.gnu.org/licenses/>          #
#                                                                              #
# On Debian systems, the complete text of the GNU General Public License       #
# version 2 and 3 can be found in                                              #
# "/usr/share/common-licenses/GPL-2" and "/usr/share/common-licenses/GPL-3".   #
#                                                                              #
################################################################################

use strict;
use warnings;

my ($exec, $longname) = ("kstars", "KStars");

use Getopt::Long;
Getopt::Long::Configure('bundling');

my ($quiet, $verbose, $debug, $help, $version);

GetOptions(
	'q|quiet'	=> \$quiet,
	'v|verbose'	=> \$verbose,
	'd|debug'	=> \$debug,
	'h|help'	=> \$help,
	'V|version'	=> \$version,
) or print STDERR "Try `$exec --help' for more information.\n" and exit 2;

$quiet = 0 if $verbose || $debug;
$quiet = $quiet ? "-q" : "";
$verbose = $verbose ? "-v" : "";
$debug = $debug ? "-d" : "";

exit if (! @ARGV);
exit if (! `which $exec`);

my ($LATITUDE, $LONGITUDE, $ELEVATION, $CITY, $REGION, $COUNTRY, $COUNTRYCODE, $TIMEZONE, $TZABBR, $TZOFFSET, $TZDSTOFFSET, $ICAO) = @ARGV;

sub main {
	exit if !$<;
	&setlocation;
}

sub setlocation {

	print "Configuring $longname...\n" if $verbose;

	# Convert to app-specific data format.

	my ($D, $M, $S, $H);

	($D, $M, $S, $H) = Dec2DMS("$LATITUDE");
	$S = sprintf("%02d", $S);
	$H = ($H eq "+") ? "N" : "S";
	my $LatDMS = "${D} : ${M} : $S : $H";

	($D, $M, $S, $H) = Dec2DMS("$LONGITUDE");
	$S = sprintf("%02d", $S);
	$H = ($H eq "+") ? "E" : "W";
	my $LongDMS = "${D} : ${M} : $S : $H";

	my $CityStr = sprintf("%-32s", $CITY);
	my $RegionStr = sprintf("%-21s", $REGION);
	my $CountryStr = sprintf("%-21s", $COUNTRY);
	my $TZOffsetStr = sprintf("%5.1f", $TZOFFSET / 3600);
	my $DST = GetDSTRule();

	my $LocationData = "$CityStr : $RegionStr : $CountryStr : $LatDMS : $LongDMS : $TZOffsetStr : $DST\n";

	# Write down configuration data.

	my @places;
	my $placesfile = "$ENV{HOME}/.kde/share/apps/kstars/mycities.dat";
	if (-e $placesfile) {
		open FILE, "$placesfile";
		@places = <FILE>;
		close FILE;
	}

	if (!grep(/$LocationData/, @places)) {

		my $found = 0;
		for my $place (@places) {
			if ($place =~ m/^$CityStr : $RegionStr : $CountryStr/) {
				$place = $LocationData;
				$found++;
			}
		}
		if (!$found) {
			push @places, "$LocationData";
		}

		`mkdir -p $ENV{HOME}/.kde/share/apps/kstars/`;
		open FILE, ">$placesfile";
		print FILE @places;
		close FILE;
	}

	`kwriteconfig --file kstarsrc --group Location --key CityName "$CITY"`;
	`kwriteconfig --file kstarsrc --group Location --key ProvinceName "$REGION"`;
	`kwriteconfig --file kstarsrc --group Location --key CountryName "$COUNTRY"`;
}

sub GetDSTRule {

	open FILE, "/usr/share/kde4/apps/kstars/Cities.dat";
	my @Cities = <FILE>;
	close FILE;

	my ($TZRegion, $TZCity) = split /\//, $TIMEZONE;
	$TZCity =~ s/_/ /g;

	my @TZCityData = grep(/^$TZCity/, @Cities);
	@TZCityData = grep(/$TZCity/, @Cities) if !@TZCityData;
	@TZCityData = grep(/ : $COUNTRY /, @Cities) if !@TZCityData;

	my $DST = "--";
	if (@TZCityData) {
		chomp ($DST = (split / : /, $TZCityData[0])[-1]);
	}

	return $DST;
}

sub Dec2DMS {
	my $dec = $_[0];
	my $sgn = ($dec >= 0) ? "+" : "-";
	$dec = abs $dec;
	my $deg = sprintf("%1d", $dec);
	$dec = $dec - $deg;
	$dec = $dec * 60;
	my $min = sprintf("%02d", $dec);
	$dec = $dec - $min;
	$dec = $dec * 60;
	my $sec = sprintf("%05.2f", $dec);
	return ($deg, $min, $sec, $sgn);
}

main;

